.type Context = [ block:Block, rest:Context ]

// find number of jumps to estimate best context depth

.decl NumberOfJumps(n: number)

NumberOfJumps(n1 + n2) :-
   n1 = count : JUMP(_),
   n2 = count : JUMPI(_).

#define ESTIMATE_DEPTH(n) (100000 / (n) / (n))

.decl MaxContextDepth(d: number)
MaxContextDepth(2).

/*
MaxContextDepth(m) :-
   NumberOfJumps(n),
   m = ESTIMATE_DEPTH(n),
   m >= 1, m <= 5.

MaxContextDepth(1) :-
   NumberOfJumps(n),
   m = ESTIMATE_DEPTH(n),
   m < 1.

MaxContextDepth(5) :-
   NumberOfJumps(n),
   m = ESTIMATE_DEPTH(n),
   m > 5.
*/
.output MaxContextDepth

.decl InitialContext(ctx : Context)
InitialContext(nil).

.decl ContextDepth(ctx : Context, depth : number)
ContextDepth(init, 0) :-
  InitialContext(init).

.decl IsContext(rest:Context)
IsContext(init) :- InitialContext(init).

.decl DropLast(ctx : Context, newCtx : Context)
DropLast(ctx, nil) :-
  IsContext(ctx),
  ctx = [ block, nil ], block = block.

IsContext(newCtx),
DropLast(ctx, newCtx) :-
  IsContext(ctx),
  ctx = [ block, rest ],
  DropLast(rest, newRest),
  newCtx = [ block, newRest ].

// We're only interested in private functions
// No ambiguity for public functions
.decl PrivateFunctionCaller(caller: Block)

PrivateFunctionCaller(caller) :-
  PushValue(stmt, target),
  JUMPDEST(CAST_TO_SYMBOL(target)),
  PushValue(stmt2, target),
  stmt2 != stmt,
  Statement_Block(stmt, caller),
  Statement_Block(stmt2, caller2),
  caller != caller2.

/*
PrivateFunctionCaller(caller) :-
  PushValue(stmt, target),
  JUMPDEST(CAST_TO_SYMBOL(target)),
  Statement_Block(stmt, caller),
  !StaticBlockJumpTarget(caller, target).

.decl StaticBlockJumpTarget(caller: Block, target: Value)

StaticBlockJumpTarget(caller, target) :-
  ImmediateBlockJumpTarget(caller, targetVar),
  Variable_Value(targetVar, target).
*/

.decl MergeContext(ctx : Context, caller : Block, newContext : Context)

// Trivial control flow case
MergeContext(ctx, caller, ctx) :-
  ReachableContext(ctx, caller),
  !PrivateFunctionCaller(caller).

// Complex control flow case, add
ContextDepth(newContext, depth + 1),
MergeContext(ctx, caller, newContext),
IsContext(newContext) :-
  ReachableContext(ctx, caller),
  PrivateFunctionCaller(caller),
  ContextDepth(ctx, depth),
  MaxContextDepth(maxDepth),
  depth < maxDepth,
  newContext = [caller, ctx].

// Complex control flow case, drop and add
ContextDepth(newContext, depth),
MergeContext(ctx, caller, newContext),
IsContext(newContext) :-
  ReachableContext(ctx, caller),
  PrivateFunctionCaller(caller),
  ContextDepth(ctx, depth),
  MaxContextDepth(depth),
  DropLast(ctx, cutDownCtx),
  newContext = [caller, cutDownCtx].
