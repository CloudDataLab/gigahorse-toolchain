/*****
 * Function discovery logic
 *****/

/******
  * First, heuristics for discovering public functions
******/
// Special entry point to public function found heuristically
.decl PublicFunctionEntry(basicBlock: Block, sigHash: Value)
// Code inserted by compiler to dispatch to the correct public function
.decl PublicFunctionDispatcher(basicBlock: Block, sigHash: Value)
// (Possibly) Code inserted by compiler to compare function signature
.decl BlockComparesSig(block: Block, sigHash: Value)

// Compares label from stack to a constant: common public function dispatch
// pattern
BlockComparesSig(block, sigHash) :-
  Statement_Block(pushStmt, block),
  PUSH4(pushStmt, sigHash),
  Statement_Defines(pushStmt, sigHashVar),
  Statement_Block(eqStmt, block),
  EQ(eqStmt),
  Statement_Uses(eqStmt, sigHashVar, _),
  Statement_Block(jumpiStmt, block),
  JUMPI(jumpiStmt),
  Statement_Defines(eqStmt, pred),
  Statement_Uses(jumpiStmt, pred, 1).
   
// Initial case, uses CALLDATALOAD
PublicFunctionDispatcher(block, sigHash) :-
  Statement_Block(stmt, block),
  CALLDATALOAD(stmt),
  BlockComparesSig(block, sigHash).

// General case
PublicFunctionDispatcher(block, sigHash) :-
  BlockComparesSig(block, sigHash),
  BlockEdge(_, prev, _, block),
  PublicFunctionDispatcher(prev, _).

PublicFunctionEntry([stmt], sigHash) :-
  PublicFunctionDispatcher(block, sigHash),
  Statement_Block(jumpiStmt, block),
  JUMPI(jumpiStmt),
  BeforeLocalStackContents(jumpiStmt, _, labelvar),
  Variable_Value(labelvar, label),
  IsBasicBlockHead(stmt),
  ord(label) = ord(stmt).


/******
         Generic basic block Reachable-from implementation
******/

.decl ReachableFrom(block:Block, from:Block)

ReachableFrom(block, block) :- Statement_Block(_, block).

ReachableFrom(blockNext, blockPrev) :-
  ReachableFrom(block, blockPrev),
  BlockEdge(_, block, _, blockNext). 


/******
         Heuristics for detecting call-return patterns
******/

.decl PossibleReturnAddress(caller:Block, ret:Block, target:Block)

// Possibly a return:
// a) basic block jumps to a valid "non-locally-derived" address
// b) address originates at a caller that can reach the
//    returning basic block.
PossibleReturnAddress(callerBlock, retBlock, target) :-
  BlockJumpValidTarget(_, retBlock, targetVariable, target),
  BasicBlock_Tail(callerBlock, callerStmt),
  callerBlock != retBlock,
  IsJump(callerStmt),
  Statement_Defines(otherCallerStatement, targetVariable),
  Statement_Block(otherCallerStatement, callerBlock).


.decl PossibleFunctionCall(caller:Block, func:Block, retBlock:Block, retTarget:Block)

PossibleFunctionCall(caller, func, retBlock, retTarget) :-
  PossibleReturnAddress(caller, retBlock, retTarget),
  BlockJumpValidTarget(_, caller, _, func).

.decl PossiblyInFunction(block:Block, func:Block)

// Which basic blocks belong to which function
PossiblyInFunction(func, func) :-
  PossibleFunctionCall(_, func, _, _).

// In this case we're moving onto another function
PossiblyInFunction(next, func) :-
  PossiblyInFunction(block, func),
  BlockEdge(_, block, _, next),
  !PossibleFunctionCall(block, _, _, _).

// In this case we're return from a function we've just called
PossiblyInFunction(retTarget, func) :-
  PossiblyInFunction(block, func),
  PossibleFunctionCall(block, _, _, retTarget).

// What is a function?
// A function has to have a single entry point

.decl NotRegularFunction(func:Block)

// A basic block can only belong to a single function
// Solution: Pick one!
NotRegularFunction([func2]) :-
  PossiblyInFunction(block, [func1]),
  PossiblyInFunction(block, [func2]),
  ord(func1) > ord(func2).

// A function can only be entered by calling it
NotRegularFunction(func) :-
  PossiblyInFunction(_, func),
  BlockEdge(_, caller, _, func),
  !PossibleFunctionCall(caller, func, _, _).


// caller calls func and func returns to retTarget
.decl FunctionCallReturn(caller:Block, func:Block, retTarget:Block)
.decl Function_Return(func:Block, retBlock:Block)

Function_Return(func, retBlock),
FunctionCallReturn(caller, func, retTarget) :-
  PossibleFunctionCall(caller, func, retBlock, retTarget),
  !NotRegularFunction(func).


// Further function decomposition.
// Detects function boundaries, calls and returns iteratively.  Main
// heuristic: if a block is reachable by two others that have to be in
// different functions, then that block is also in an independent
// function.

// Used components to force stratification.
.comp FunctionDetection {
  // An Entry to a function (private or public)
  .decl FunctionEntry(block: Block)
  // Same, but for previous iteration of the algorithm   
  .decl PrevFunctionEntry(block: Block)

  // Basic block belongs to function
  .decl InFunction(block: Block, function: Block)
  // Basic block performs a function call 
  .decl FunctionCall(block: Block, func: Block)
   // Same, but for previous iteration of the algorithm   
  .decl PrevFunctionCall(block: Block, func: Block)

  InFunction(block, block) :-
    PrevFunctionEntry(block).

  InFunction(next, func) :-
    InFunction(block, func),
    BlockEdge(_, block, _, next),
    !PrevFunctionCall(block, next),
    !Function_Return(_, block).

 InFunction(next, func) :-
   InFunction(block, func),
   FunctionCallReturn(block, _, next).

  // Heuristic for finding function calls
  // Execution flows to a basic block that is
  // part of more than one function, TODO
  FunctionCall(prev, block),
  FunctionEntry(block) :-
    InFunction(block, func1),
    InFunction(block, func2),
    func1 != func2,
    BlockEdge(_, prev, _, block),
    !Function_Return(_, prev),
    !(InFunction(prev, func1), InFunction(prev, func2)).

  FunctionCall(prev, func1) :- PrevFunctionCall(prev, func1).
}

// Initialize and iterate the algorithm
.init detector0 = FunctionDetection
detector0.PrevFunctionEntry(block) :-
  PublicFunctionEntry(block, _).
detector0.PrevFunctionEntry(block) :-
  FunctionCallReturn(_, block, _).

detector0.PrevFunctionCall(prev, func) :-
  PublicFunctionEntry(func, sigHash),
  PublicFunctionDispatcher(prev, sigHash).

detector0.PrevFunctionCall(prev, func) :-
  FunctionCallReturn(prev, func, _).
  
.init detector1 = FunctionDetection
detector1.PrevFunctionEntry(block) :- detector0.FunctionEntry(block).
detector1.PrevFunctionCall(block, func) :- detector0.FunctionCall(block, func).

.init detector2 = FunctionDetection
detector2.PrevFunctionEntry(block) :- detector1.FunctionEntry(block).
detector2.PrevFunctionCall(block, func) :- detector1.FunctionCall(block, func).

/***********
   Collecting all function decomposition results
***********/

.decl FunctionCall(block:Block, func:Block)

// Case 1: private call return
FunctionCall(block, func) :- FunctionCallReturn(block, func, _).

// Case 2: dispatch public functions
FunctionCall(block, func) :-
  PublicFunctionEntry(func, sigHash),
  PublicFunctionDispatcher(block, sigHash).

// Case 3: further decomposition using components
FunctionCall(block, func) :-
  detector0.FunctionCall(block, func) ;
  detector1.FunctionCall(block, func) ;
  detector2.FunctionCall(block, func).

.decl Function(func:Block)

// Case 1: Smart contract entry
Function(["0x0"]).

// Remaining cases handled by inputs/outputs of componenets
Function(func) :-
  detector0.PrevFunctionEntry(func) ;
  detector0.FunctionEntry(func) ;
  detector1.FunctionEntry(func) ;
  detector2.FunctionEntry(func).


/***********
 * Discovery of arguments accepted by a function   
 ***********/

// Intra-function edges (i.e., real CFG edges)
.decl LocalBlockEdge(block: Block, next: Block)

LocalBlockEdge(block, next) :-
  BlockEdge(_, block, _, next),
  !Function(next),
  !Function_Return(_, block).

// REVIEW: basic blocks are interrupted at function call instructions
// (unlike, say, LLVM IR) but we connect the block to the one after
// the function return.
LocalBlockEdge(block, next) :-
  BlockEdge(_, block, _, func),
  FunctionCallReturn(block, func, next).

// New Instructions
// CALLPRIVATE(stmt, function)
// CALLPRIVATEI(stmt, cond, function)
// RETURNPRIVATE(stmt)
// RETURNPRIVATEI(stmt, cond)

.decl InFunction(block: Block, func: Block)
 
InFunction(block, block) :-
  Function(block).

InFunction(next, func) :-
  InFunction(block, func),
  BlockEdge(_, block, _, next),
  !FunctionCall(block, next),
  !Function_Return(_, block).

InFunction(next, func) :-
  InFunction(block, func),
  FunctionCallReturn(block, _, next).

.decl Function_ReachableBlocks(func: Block, block: Block)

Function_ReachableBlocks(func, block) :-
   InFunction(block, func).

Function_ReachableBlocks(func, other) :-
  Function_ReachableBlocks(func, block),
  FunctionCall(block, callee),
  Function_ReachableBlocks(callee, other).

.decl Function_Uses(func: Block, var: Variable)

Function_Uses(func, var) :-
   Function_ReachableBlocks(func, block),
   Statement_Block(stmt, block),
   Statement_Uses(stmt, var, _).

.decl Function_Defines(func: Block, var: Variable)

Function_Defines(func, var) :-
   Function_ReachableBlocks(func, block),
   Statement_Block(stmt, block),
   Statement_Defines(stmt, var).


// This is used to calculate an upper bound of the number of arguments
// for a function. A more accurate number of arguments is calculated based
// on the PerFunctionBlock*Contents information
.decl ImpossibleFunctionArguments(func: Block, alias: Alias)

ImpossibleFunctionArguments(func, alias) :-
   Function(func),
   BlockInputContents(_, func, alias, var),
   !Function_Uses(func, var).

ImpossibleFunctionArguments(func, n+1) :-
   Function(func),
   n = max alias: BlockInputContents(_, func, alias, _).

.decl NumberOfFunctionArguments(func: Block, num: Alias)

NumberOfFunctionArguments(func, n) :-
   n = min alias: ImpossibleFunctionArguments(func, alias),
   Function(func).

// This is used to calculate an upper bound of the number of variables
// a function may return.
.decl ImpossibleFunctionReturnArguments(func: Block, alias: Alias)

ImpossibleFunctionReturnArguments(func, alias) :-
   BlockOutputContents(_, func, alias, var),
   !Function_Uses(func, var),
   !Function_Defines(func, var).

ImpossibleFunctionReturnArguments(func, n+1) :-
   Function(func),
   n = max alias: BlockOutputContents(_, func, alias, _).

.decl NumberOfFunctionReturnArguments(func: Block, num: Alias)

NumberOfFunctionReturnArguments(func, n) :-
   n = min alias: ImpossibleFunctionReturnArguments(func, alias),
   Function(func).

// Renaming variables in functions
.decl PerFunctionBlockInputContents(block:Block, index:number, var:Variable)
.decl PerFunctionBlockOutputContents(block:Block, index:number, var:Variable)

PerFunctionBlockInputContents([funcStmt], alias, newVar) :-
  NumberOfFunctionArguments([funcStmt], n),
  IsAliasLessThan(alias, n),
  FRESH_VARIABLE(newVar, funcStmt, alias).


PerFunctionBlockInputContents(nextBlock, index, variable) :-
  PerFunctionBlockOutputContents(block, index, variable),
  LocalBlockEdge(block, nextBlock).

// TODO: There are three cases
// 1) Normal block to block flow, i.e. no function calls from block to block
PerFunctionBlockOutputContents(caller, index, variable) :-
  BeforeFunctionCallPerFunctionBlockOutputContents(caller, index, variable),
  !FunctionCall(caller, _).

// 2) First block calls a function, take return args from function call

PerFunctionBlockOutputContents(caller, alias, newVar) :-
  FunctionCall(caller, func),
  NumberOfFunctionReturnArguments(func, n),
  IsAliasLessThan(alias, n),
  BasicBlock_Tail(caller, callStmt),
  FRESH_VARIABLE(newVar, callStmt, alias).


// 3) First block calls a function, take any remaining variables that have
//    not been passed as arguments, and shift
PerFunctionBlockOutputContents(caller, newIndex, variable) :-
  FunctionCall(caller, func),
  NumberOfFunctionReturnArguments(func, n_out),
  NumberOfFunctionArguments(func, n_in),
  BeforeFunctionCallPerFunctionBlockOutputContents(caller, index, variable),
  index >= n_in,
  newIndex = index+n_out-n_in,
  newIndex >= 0.

.decl BeforeFunctionCallPerFunctionBlockOutputContents(caller: Block, index: Alias, variable: Variable)


// Copy the stack variables untouched by the callee basic block
BeforeFunctionCallPerFunctionBlockOutputContents(callee, index+delta, variable) :-
  PerFunctionBlockInputContents(callee, index, variable),
  index >= calleePopLen,
  index+delta <= MAX_STACK_HEIGHT,
  BlockPopDelta(callee, calleePopLen),
  BlockStackDelta(callee, delta).

// Handle the proper variables pushed by this basic block
BeforeFunctionCallPerFunctionBlockOutputContents(block, index, var) :-
  InFunction(block, _),
  BasicBlock_Tail(block, stmt),
  LocalStackContents(stmt, index, var),
  CheckIsVariable(var).

// Handle the aliases pushed by this basic block
BeforeFunctionCallPerFunctionBlockOutputContents(block, index, realVariable) :-
  PerFunctionBlockInputContents(block, alias, realVariable),
  BasicBlock_Tail(block, stmt),
  LocalStackContents(stmt, index, alias),
  CheckIsAlias(alias).

.output PerFunctionBlockInputContents
.output PerFunctionBlockOutputContents
